plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id("com.github.johnrengelman.shadow") version "8.1.1"
}

group = 'org.fetarute'
version = '0.0.1-beta'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "bergerkiller-repo"
        url = "https://ci.mg-dev.eu/plugin/repository/everything/"
    }
}

dependencies {
    implementation platform("com.bergerkiller.bukkit:BKCommonLib-bom:${bkCommonLibVersion}")
    implementation("net.kyori:adventure-platform-bukkit:${adventurePlatformVersion}")
    implementation("dev.triumphteam:triumph-gui:${triumphGuiVersion}")
    compileOnly("io.papermc.paper:paper-api:${paperApiVersion}")
    compileOnly("com.bergerkiller.bukkit:BKCommonLib:${bkCommonLibVersion}")
    compileOnly("com.bergerkiller.bukkit:TrainCarts:${trainCartsVersion}")
    compileOnly("org.incendo:cloud-paper")
    compileOnly("org.incendo:cloud-minecraft-extras")
    testImplementation("io.papermc.paper:paper-api:${paperApiVersion}")
    testImplementation("com.bergerkiller.bukkit:BKCommonLib:${bkCommonLibVersion}")
    testImplementation("com.bergerkiller.bukkit:TrainCarts:${trainCartsVersion}")
    testImplementation("org.junit.jupiter:junit-jupiter:5.10.2")
    testImplementation("org.mockito:mockito-core:5.20.0")
    testImplementation("org.mockito:mockito-inline:5.2.0")
}

tasks {
    runServer {
        // Configure the Minecraft version for our task.
        // This is the only required configuration besides applying the plugin.
        // Your plugin's jar (or shadowJar if present) will be used automatically.
        minecraftVersion(project.minecraftVersion)
    }
}

tasks.test {
    useJUnitPlatform()
}

def targetJavaVersion = 17
def gitCommitProvider = providers.exec {
    commandLine 'git', 'rev-parse', '--short', 'HEAD'
}.standardOutput.asText.map { it.trim() }
def gitCommit = gitCommitProvider.getOrElse("unknown")
def buildTimestamp = new Date().format("yyMMdd")
def buildId = "build-${gitCommit}-${buildTimestamp}"

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

shadowJar {
    archiveClassifier.set("")
    def commonPrefix = "com.bergerkiller.bukkit.common.dep"
    relocate("cloud.commandframework", "${commonPrefix}.cloud.commandframework")
    relocate("org.incendo.cloud", "${commonPrefix}.cloud")
    relocate("io.leangen.geantyref", "${commonPrefix}.typetoken")
    relocate("me.lucko.commodore", "${commonPrefix}.me.lucko.commodore")
}

tasks.named("build") {
    dependsOn("shadowJar")
}

tasks.named("jar") {
    enabled = false
}

processResources {
    def props = [version: version, gitCommit: gitCommit, buildId: buildId]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
    filesMatching('build-info.properties') {
        expand props
    }
}
